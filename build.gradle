
import org.opensearch.gradle.info.BuildParams
import java.util.concurrent.Callable


buildscript {
    ext {
        opensearch_version = System.getProperty("opensearch.version", "1.3.0-SNAPSHOT")
        knn_bwc_version = System.getProperty("bwc.version", "1.2.0.0-SNAPSHOT")
        opensearch_bwc_version = "${knn_bwc_version}" - ".0-SNAPSHOT"
    }

    repositories {
        mavenLocal()
        maven { url "https://aws.oss.sonatype.org/content/repositories/snapshots" }
        mavenCentral()
    }

    dependencies {
        classpath "org.opensearch.gradle:build-tools:${opensearch_version}"
    }
}

plugins {
    id 'nebula.ospackage' version "8.3.0"
    id 'java-library'
    id "io.freefair.lombok" version "5.0.0-rc4"
    id 'jacoco'
    id "com.diffplug.spotless" version "6.3.0" apply false
}

apply from: 'gradle/formatting.gradle'
apply plugin: 'opensearch.opensearchplugin'
apply plugin: 'opensearch.rest-test'

group 'org.example'
version '1.3.0-SNAPSHOT'


repositories {
    mavenLocal()
    maven { url "https://aws.oss.sonatype.org/content/repositories/snapshots" }
    mavenCentral() // For Elastic Libs that you can use to get started coding until open OpenSearch libs are available

}

ext {
    isSnapshot = "true" == System.getProperty("build.snapshot", "true")
}

allprojects {
    version = opensearch_version - "-SNAPSHOT" + ".0"
    if (isSnapshot) {
        version += "-SNAPSHOT"
    }

    plugins.withId('java') {
        sourceCompatibility = targetCompatibility = "1.8"
    }
}

ext {
    projectSubstitutions = [:]
    licenseFile = rootProject.file('LICENSE.txt')
    noticeFile = rootProject.file('NOTICE.txt')
}

opensearchplugin {
    description 'OpenSearch k-NN plugin'
    classname 'org.opensearch.knn.plugin.RunPlugin'
}


licenseHeaders.enabled = true
dependencyLicenses.enabled = false
testingConventions.enabled = false
thirdPartyAudit.enabled = false
validateNebulaPom.enabled = false
loggerUsageCheck.enabled = false

subprojects {
    repositories {
        mavenLocal()
        maven { url "https://aws.oss.sonatype.org/content/repositories/snapshots" }
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }
}

versions << [
        'aws': '1.11.749'
]

dependencies {
    compile "org.opensearch:opensearch:${opensearch_version}"
    compileOnly "org.opensearch.plugin:opensearch-scripting-painless-spi:${versions.opensearch}"
    compile group: 'com.google.guava', name: 'failureaccess', version:'1.0.1'
    compile group: 'com.google.guava', name: 'guava', version:'30.0-jre'
    compile group: 'commons-lang', name: 'commons-lang', version: '2.6'
    testImplementation "org.opensearch.test:framework:${opensearch_version}"
    api "com.amazonaws:aws-java-sdk-s3:${versions.aws}"
    api "com.amazonaws:aws-java-sdk-core:${versions.aws}"
    api "com.amazonaws:jmespath-java:${versions.aws}"
    api "org.apache.httpcomponents:httpclient:${versions.httpclient}"
    api "org.apache.httpcomponents:httpcore:${versions.httpcore}"
    api "commons-logging:commons-logging:${versions.commonslogging}"
    api "org.apache.logging.log4j:log4j-1.2-api:${versions.log4j}"
    api "commons-codec:commons-codec:${versions.commonscodec}"
    api "com.fasterxml.jackson.core:jackson-core:${versions.jackson}"
    api "com.fasterxml.jackson.core:jackson-databind:${versions.jackson}"
    api "com.fasterxml.jackson.core:jackson-annotations:${versions.jackson}"
    api "com.fasterxml.jackson.dataformat:jackson-dataformat-cbor:${versions.jackson}"
    api "joda-time:joda-time:${versions.joda}"

    // HACK: javax.xml.bind was removed from default modules in java 9, so we pull the api in here,
    // and whitelist this hack in JarHell
    api 'javax.xml.bind:jaxb-api:2.2.2'

}

tasks.named("dependencyLicenses").configure {
    mapping from: /aws-java-sdk-.*/, to: 'aws-java-sdk'
    mapping from: /jmespath-java.*/, to: 'aws-java-sdk'
    mapping from: /jackson-.*/, to: 'jackson'
    mapping from: /jaxb-.*/, to: 'jaxb'
}

forbiddenApis.ignoreFailures = true
forbiddenPatterns {
    setEnabled(false)
}


compileJava.options.warnings = false
compileJava {
    options.compilerArgs.addAll(["-processor", 'lombok.launch.AnnotationProcessorHider$AnnotationProcessor'])
    doFirst {
        // TODO: do not fail build on warnings, need to fix all compiler warnings
        options.compilerArgs.remove('-Werror')
        // TODO: need to fix all java doc format
        options.compilerArgs.remove('-Xdoclint:all')
    }
}
// TODO: Similarly, need to fix compiling errors in test source code
compileTestJava.options.warnings = false
compileTestJava {
    options.compilerArgs.addAll(["-processor", 'lombok.launch.AnnotationProcessorHider$AnnotationProcessor'])
    doFirst {
        options.compilerArgs.remove('-Werror')
        options.compilerArgs.remove('-Xdoclint:all')
    }
}
